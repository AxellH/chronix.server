dependencies {
    testCompile 'org.apache.commons:commons-lang3:3.1'
    testCompile 'commons-codec:commons-codec:1.8'
    testCompile 'org.apache.solr:solr-solrj:5.3.1'

    testCompile 'de.qaware.chronix:chronix-api:0.0.2'
    testCompile 'de.qaware.chronix:chronix-kassiopeia-simple:0.0.2'
    testCompile 'de.qaware.chronix:chronix-kassiopeia-simple-converter:0.0.2'


    testCompile project(':chronix-server-client')
    testCompile project(':chronix-test-extensions')

    testCompile project(':chronix-server-query-handler')
    testCompile project(':chronix-server-retention')
}


import de.undercouch.gradle.tasks.download.Download

task downloadZipFile(type: Download) {
    onlyIfNewer true
    src 'http://archive.apache.org/dist/lucene/solr/5.3.1/solr-5.3.1.zip'
    dest new File(buildDir, 'solr-5.3.1.zip')
}

task downloadAndUnzipFile(dependsOn: downloadZipFile, type: Copy) {
    from zipTree(downloadZipFile.dest)
    into buildDir
}
task copyCoreToSolr(dependsOn: downloadAndUnzipFile, type: Copy) {
    into "$buildDir/solr-5.3.1/server/solr"
    from "$projectDir/src/test/resources/de/qaware/chronix/"
}

task copyIntegrationRuntimeLibs(dependsOn: copyCoreToSolr, type: Copy) {
    into "$buildDir/solr-5.3.1/server/solr/chronix/lib"
    from(configurations)
    include("*chronix-api*.jar")
    include("*chronix-kassiopeia*.jar")
    include("*chronix-server-*.jar")
    include("*quartz*.jar")
    include("*gson*.jar")
}

processTestResources.dependsOn copyIntegrationRuntimeLibs

/**
 * Helper task
 */
class ExecWait extends DefaultTask {
    String command
    String ready
    String directory

    @TaskAction
    def spawnProcess() {

        ProcessBuilder builder = new ProcessBuilder(command.split(' '))
        builder.redirectErrorStream(true)
        builder.directory(new File(directory))
        Process process = builder.start()

        InputStream stdout = process.getInputStream()
        BufferedReader reader = new BufferedReader(new InputStreamReader(stdout))

        def line
        while ((line = reader.readLine()) != null) {
            println line
            if (line.contains(ready)) {
                println "$command is ready"
                break;
            }
        }
    }
}

/**
 * Tasks to start and stop solr
 */
String osName = System.getProperty("os.name").toLowerCase();
task startSolr(dependsOn: copyIntegrationRuntimeLibs, type: ExecWait) {
    if (osName.contains("windows")) {
        command 'cmd.exe /C solr.cmd start'
        ready "Started Solr server on port 8983. Happy searching!"
        directory "$buildDir/solr-5.3.1/bin/"
    } else {
        command 'bash solr start'
        ready "Started Solr server on port 8983. Happy searching!"
        directory "$buildDir/solr-5.3.1/bin/"
    }
}

task stopSolr(type: ExecWait) {
    if (osName.contains("windows")) {
        command 'cmd.exe /C solr.cmd stop -p 8983'
        ready ""
        directory "$buildDir/solr-5.3.1/bin/"
    } else {
        command 'bash solr stop -p 8983'
        ready ""
        directory "$buildDir/solr-5.3.1/bin/"
    }
}

task deleteUnusedDirs(type: Delete) {

    sleep(30_000)
    //delete the following dirs
    def solrDir = "$buildDir/solr-5.3.1/"

    delete "$solrDir/docs"
    delete "$solrDir/example"
    delete "$solrDir/dist/solrj-lib"
    delete "$solrDir/dist/test-framework"
    delete "$solrDir/dist/solr-solrj-5.3.1.jar"
    delete "$solrDir/dist/solr-test-framework-5.3.1.jar"

    delete fileTree("$solrDir/server/logs").include("**/*")
    delete "$solrDir/server/solr/configsets"
    delete fileTree("$solrDir/server/solr").include("hsperfdata*")
}


task buildRelease(dependsOn: deleteUnusedDirs, type: Zip) {
    def solrDir = "$buildDir/solr-5.3.1/"
    from("$solrDir")
    into 'chronix-solr-5.3.1' // note that this specifies path *in* the archive
    destinationDir file("chronix-${project.version}") // directory that you want your archive to be placed in
    baseName = "chronix"
}

test.dependsOn startSolr

test.finalizedBy stopSolr